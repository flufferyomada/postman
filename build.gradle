buildscript {
	repositories {
		gradlePluginPortal()
		maven {
			name = "Fabric"
			url = "https://maven.fabricmc.net/"
		}
		flatDir {
			dirs 'libs'
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:${loom_version}"
		classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
	}
}

apply plugin: "fabric-loom"
apply plugin: "com.github.johnrengelman.shadow"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

configurations {
	shadow
	modImplementation.extendsFrom shadow
}

repositories {
	maven {
		name = "lukflug"
		url = "https://lukflug.github.io/maven/"
	}

	maven {
		name = 'Ladysnake Mods'
		url = 'https://maven.ladysnake.org/releases' // ladysnake.jfrog.io/artifactory/mods
		content {
			includeGroup 'io.github.ladysnake' // 1.20.1 is org.ladysnake ( same for dependencies below)
			includeGroupByRegex 'io\\.github\\.onyxstudios.*'
		}
	}
}

loom{
	accessWidenerPath = file("src/main/resources/postman.accesswidener")
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"

	modImplementation "io.github.ladysnake:satin:${satin_version}"
	include "io.github.ladysnake:satin:${satin_version}"

	shadow "com.lukflug:panelstudio:${panelstudio_version}"
	shadow "com.lukflug:panelstudio-mc20:${panelstudio_versionmc}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

shadowJar {
	classifier = "dev"
	configurations = [project.configurations.shadow]
	relocate "com.lukflug.panelstudio","com.lukflug.panelstudio_0_2_4"
}

remapJar {
	dependsOn shadowJar
	inputFile = shadowJar.archiveFile
}
